ToolMask = bit.bor( CONTENTS_SOLID, CONTENTS_MOVEABLE, CONTENTS_MONSTER, CONTENTS_WINDOW, CONTENTS_DEBRIS, CONTENTS_GRATE, CONTENTS_AUX )
ToolDistance = 4096 * 8

hook.Add "CanTool", "Player Remover", ( _, toolName ) =>
    unless toolName == "remover" and @IsAdmin!
        return

    weapon = @GetActiveWeapon!
    unless weapon and weapon\IsValid! and weapon\GetClass! == "gmod_tool"
        return

    isLagCompensated = weapon\IsLagCompensated!
    weapon\SetLagCompensated( true )
    @LagCompensation( true )

	trace = {
        start: @EyePos!
        mask: ToolMask
        filter: @
    }

	trace.endpos = trace.start + ( @GetAimVector! * ToolDistance )
	traceResult = util.TraceLine( trace )

    @LagCompensation( false )
    weapon\SetLagCompensated( isLagCompensated )

	unless traceResult.Hit
        return

    target = traceResult.Entity
    unless target\IsValid! and target\IsPlayer! and target\Alive!
        return

    if target\GetNoDraw! or target\IsDormant!
        return

    weapon\DoShootEffect( traceResult.HitPos, traceResult.HitNormal, target, traceResult.PhysicsBone, IsFirstTimePredicted! )

    if @GetInfo( "cl_player_remover_kick" ) == "1"
        target\Kick( "You have been removed." )
    else
        target\KillSilent!

    effectData = EffectData!
    effectData\SetOrigin( target\GetPos! )
    effectData\SetEntity( target )
    util.Effect( "entity_remove", effectData, true, true )

    return false