hook.Add "CanTool", "Player Remover", ( traceResult, toolName ) =>
    unless toolName == "remover" and @IsAdmin!
        return

    target = traceResult.Entity
    unless target\IsValid! and target\IsPlayer! and target\Alive!
        return

    if target\GetNoDraw! or target\IsDormant!
        return

    if @GetInfo( "cl_player_remover_kick" ) == "1"
        target\Kick( "You have been removed." )
    else
        target\KillSilent!

    effectData = EffectData!
    effectData\SetOrigin( target\GetPos! )
    effectData\SetEntity( target )
    util.Effect( "entity_remove", effectData, true, true )

    weapon = @GetActiveWeapon!
    if weapon and weapon\IsValid! and weapon\GetClass! == "gmod_tool"
        weapon\DoShootEffect( traceResult.HitPos, traceResult.HitNormal, target, traceResult.PhysicsBone, IsFirstTimePredicted! )

    return false